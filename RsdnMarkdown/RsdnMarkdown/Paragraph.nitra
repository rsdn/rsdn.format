namespace RsdnMarkdown
{
  syntax module ParagraphSyntax
  {
    using PrettyPrint;
    using Outline;
    using TokenNames;
    using StandardSpanClasses;
    using Whitespaces;
    using Identifiers;
    using CStyleComments;

    using Nitra;
    using RsdnMarkdown.Utils;

    using System.IO;
    using System.Net;

    regex W               = Whitespace;  // without new line
    regex Ws              = Whitespace+; // without new line
    regex WsOpt           = Whitespace*; // without new line
    regex Esc             = "\\";
    regex UnorderedListItemPrefix = ("#" | "№")+;
    regex OrderedListItemPrefix = "*"+;
    regex QuotePrefix = IdentifierStartCharacter* ">"+;

    [ExplicitSpaces]
    token PartContent = Content=(!NewLine Any)*
    {
      ToHtml(tag : string, writer : TextWriter) : void = ProcessContent(tag, writer, this, Content);
    }

    [ExplicitSpaces]
    token Header = Ws !W !NewLine Header=(!NewLine Any)+;

    [StartRule]
    [ExplicitSpaces]
    syntax Start = Content* !Any;

    token End
    {
      | NewLine
      | Eof = !Any
    }

    [ExplicitSpaces]
    syntax Content
    {
      | Formated = Part End
      | Paragraph = !Part Content=(!NewLine Any)* End
    }

    [ExplicitSpaces]
    syntax Content2
    {
      | Formated = Part
      | Paragraph = Content=(!Part !NewLine Any)*
    }

    [StartRule]
    [ExplicitSpaces]
    syntax Part
    {
      | H1                = "="                     Ws    PartContent
      | H2                = "=="                    Ws    PartContent
      | H3                = "==="                   Ws    PartContent
      | H4                = "===="                  Ws    PartContent
      | H5                = "====="                 Ws    PartContent
      | H6                = "======"                Ws    PartContent
      | TagLine           = "@@@"                   Ws    PartContent
      | UnorderedListItem = UnorderedListItemPrefix Ws    PartContent
      | OrderedListItem   = OrderedListItemPrefix   Ws    PartContent
      | Hr                = "---"                   WsOpt
      | Blockquote        = "<\"" Header? WsOpt NewLine PartContent* Token WsOpt
        {
          regex Token = "\">";
          [ExplicitSpaces] syntax PartContent = !Token Content;
        }
      | FoldedBlock       = "<+"  Header  WsOpt NewLine PartContent* Token WsOpt
        {
          regex Token = "+>";
          [ExplicitSpaces] syntax PartContent = !Token Content;
        }
      | Moderator         = "<!"          WsOpt NewLine PartContent* Token WsOpt
        {
          regex Token = "!>";
          [ExplicitSpaces] syntax PartContent = !Token Content;
        }
    }
  }
}